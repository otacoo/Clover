import java.time.format.DateTimeFormatter

apply plugin: 'com.android.application'

// Code style
apply plugin: 'checkstyle'
tasks.register('checkstyle', Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'
    classpath = files()
}


afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
}

android {
    //noinspection GradleDependency
    compileSdk 34
    namespace "org.otacoo.chan"

    defaultConfig {
        applicationId "org.otacoo.chan"
        minSdk 28
        //noinspection ExpiredTargetSdkVersion
        targetSdk 29
        //noinspection ExpiredTargetSdkVersion
        multiDexEnabled true

        versionName "v3.0.25"
        // of the format XXYYZZ, where XX is major, YY is minor, ZZ is patch
        // (watch out for octal notation, never start with a 0)
        versionCode 30025
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    /*
    kotlinOptions {
        jvmTarget = '17'
    }
    */

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        buildConfig true
    }

    def formatter = DateTimeFormatter.ofPattern("yyyyMMdd_HHmm").withZone(ZoneId.from(ZoneOffset.UTC))
    def timestamp = formatter.format(Instant.now())

    /*
    If you want to sign releases, make a file in app/keys.properties with the following content:
    keystoreFile=yourkey.store
    keystorePass=pass
    keyAlias=alias
    keyPass=pass
    */

    File f = file('keys.properties')
    boolean doSign = f.exists()

    if (doSign) {
        Properties props = new Properties()
        InputStream is = new FileInputStream(f)
        props.load(is)

        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }

        is.close()
    }

    flavorDimensions "default"
    productFlavors {
        // The app name refers to the name as displayed on the launcher.
        "default" {
            dimension "default"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", "(build $timestamp)"
            buildConfigField "String", "BUILD_HASH", "\"$timestamp\""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://raw.githubusercontent.com/otacoo/Clover/main/docs/update_api.json\""
        }

        dev {
            dimension "default"
            resValue "string", "app_name", "Clover"
            resValue "string", "app_flavor_name", "(build $timestamp-dev)"
            buildConfigField "String", "BUILD_HASH", "\"\""
            buildConfigField "String", "UPDATE_API_ENDPOINT", "\"\""
        }
    }

    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            buildConfigField "boolean", "DEVELOPER_MODE", "false"
        }

        debug {
//            minifyEnabled true
//            proguardFiles 'proguard.cfg'
            buildConfigField "boolean", "DEVELOPER_MODE", "true"
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all {
                String ver = variant.mergedFlavor.versionName
                if (variant.flavorName == "default") {
                    outputFileName = new File("Clover-" + ver + ".apk")
                } else {
                    outputFileName = new File("Clover-" + ver + "-" + variant.flavorName + ".apk")
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.media3:media3-exoplayer:1.2.1'
    implementation 'androidx.media3:media3-exoplayer-dash:1.2.1'
    implementation 'androidx.media3:media3-ui:1.2.1'
    implementation 'com.android.volley:volley:1.2.1'

    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:3.12.13'
    //noinspection GradleDependency
    implementation 'com.j256.ormlite:ormlite-core:4.48'
    //noinspection GradleDependency
    implementation 'com.j256.ormlite:ormlite-android:4.48'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.31'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    //noinspection GradleDependency
    implementation 'de.greenrobot:eventbus:2.4.0'
    implementation 'org.nibor.autolink:autolink:0.12.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'
    implementation 'org.codejargon.feather:feather:1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.27.0'
}
